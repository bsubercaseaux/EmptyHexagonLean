We start by identifying points with elements of $\mathbb{R}^2$. Concretely, ~\lstinline|abbrev Point := EuclideanSpace ℝ (Fin 2)|.
The next step is to define what it means for a $k$-gon to be \emph{empty} (with respect to a set of points) and \emph{convex}, which we do in terms of \textsf{mathlib} primitives.

\begin{lstlisting}
/-- `EmptyShapeIn S P` means that `S` carves out an empty shape in `P`:
the convex hull of `S` contains no point of `P`
other than those already in `S`. -/
def EmptyShapeIn (S P : Set Point) : Prop :=
    ∀ p ∈ P \ S, p ∉ convexHull ℝ S

/-- `ConvexPoints S` means that `S` consists of extremal points of its convex hull,
i.e. the point set encloses a convex polygon. -/
def ConvexPoints (S : Set Point) : Prop :=
    ∀ a ∈ S, a ∉ convexHull ℝ (S \ {a})

def ConvexEmptyIn (S P : Set Point) : Prop :=
    ConvexPoints S ∧ EmptyShapeIn S P

def HasConvexEmptyKGon (k : Nat) (S : Set Point) : Prop :=
    ∃ s : Finset Point, s.card = k ∧ ↑s ⊆ S ∧ ConvexEmptyIn s S
\end{lstlisting}

Assume for now\footnote{The definition is presented in~\Cref{sec:triple-orientations}.} a predicate \lstinline|PointListInGeneralPosition| that states that a list of points is in \emph{general position}, i.e., no three points lie on a common line.
With this we can already state the main theorem of our paper.

\begin{lstlisting}
theorem hole_6_theorem (pts : List Point) (gp : PointListInGeneralPosition pts)
    (h : pts.length ≥ 30) : HasConvexEmptyKGon 6 pts.toFinset 
\end{lstlisting}

At the root  of the encoding of Heule and Scheucher is the idea that the~\lstinline|ConvexEmptyIn| predicate can be determined by analyzing only triangles. In particular, that a set $s$ of $k$ points in a pointset $S$ form an empty convex $k$-gon if and only if all the ${k choose 3}$ triangles induced by vertices in $s$ are empty with respect to $S$. This is discussed informally in~\cite[Section 3, Eq. 4]{emptyHexagonNumber}.
Concretely, we prove the following theorem:
\begin{lstlisting}
theorem ConvexEmptyIn.iff_triangles {s : Finset Point} {S : Set Point}
    (sS : ↑s ⊆ S) (sz : 3 ≤ s.card) :
    ConvexEmptyIn s S ↔ 
    ∀ (t : Finset Point), t.card = 3 → t ⊆ s → ConvexEmptyIn t S
\end{lstlisting}
\input{fig_triangulation.tex}
\begin{proof}[Proof sketch]
    The proof is illustrated in~\Cref{fig:triangulation}.
    The forward direction is easy to see, as triangles are always convex, and if $s$ is empty w.r.t. $S$, then so are the triangles induced by vertices of $s$.
    Let $T = \{t_1, \ldots, t_{{k \choose 3}}\}$ be all the triangles induced by vertices of $s$.
   For the backward direction we will need a \emph{triangulation lemma} stating that the convex hull of $s$ can be partitioned into triangles $P = \{t_i, \ldots, t_j\}$, and $P \subseteq T$.
    To see that if every $t \in T$ is empty w.r.t. $S$ then $s$ is also empty w.r.t. $S$ we can use the contrapositive statement.
     Indeed, assume $s$ is not empty w.r.t. $S$, there is a point $p \in S \setminus s$ that lies inside the convex hull of $s$. Because $P$ is a partition of the convex hull of $s$, point $p$ must be inside some $t \in P \subseteq T$. 
     To see convexity, we can reason contrapositively again. If $s$ is not convex, then there is a point $p \in s$ that lies inside the convex hull of $s$, and thus lies inside a triangle $t \in P$. 
\end{proof}

The next section shows how boolean variables can be used to encode which triangles are empty w.r.t. a pointset, which as the previous theorem shows, can be used to encode the presence or abscence of $k$-holes.
% \begin{proof}[Human proof sketch]

% \end{proof}



